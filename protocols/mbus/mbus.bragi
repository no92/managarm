namespace "managarm::mbus";

// Generic

enum Error {
	SUCCESS = 0,
	NO_SUCH_ENTITY
}

enum ItemType {
	STRING,
	ARRAY
}

struct AnyItem {
	ItemType type;
	tags {
		tag(1) string string_item;
		tag(2) AnyItem[] items;
	}
}

struct Property {
	string name;
	AnyItem item;
}

enum FilterType {
	EQUALS,
	CONJUNCTION,
	DISJUNCTION
}

struct AnyFilter {
	FilterType type;
	tags {
		tag(1) string path;
		tag(2) string value;
		tag(3) AnyFilter[] operands;
	}
}

struct Entity {
	int64 id;
	string name;
	Property[] properties;
}


message CreateObjectRequest 1 {
head(128):
tail:
	string name;
	Property[] properties;
}

message CreateObjectResponse 2 {
head(128):
	Error error;
	int64 id;
}


message GetPropertiesRequest 3 {
head(128):
	int64 id;
}

message GetPropertiesResponse 4 {
head(128):
	Error error;
tail:
	Property[] properties;
}


message GetRemoteLaneRequest 5 {
head(128):
	int64 id;
}

message GetRemoteLaneResponse 6 {
head(128):
	Error error;
}


message EnumerateRequest 7 {
head(128):
	uint64 seq;
tail:
	AnyFilter filter;
}

message EnumerateResponse 8 {
head(128):
	Error error;
	uint64 out_seq;    // Sequence of last entity we returned
	uint64 actual_seq; // Actual latest sequence (out_seq != actual_seq means request was split).
tail:
	Entity[] entities;
}


message ServeRemoteLaneRequest 9 {
head(128):
}

message ServeRemoteLaneResponse 10 {
head(128):
	Error error;
}


message UpdatePropertiesRequest 11 {
head(128):
	int64 id;
tail:
	Property[] properties;
}

message UpdatePropertiesResponse 12 {
head(128):
	Error error;
}
